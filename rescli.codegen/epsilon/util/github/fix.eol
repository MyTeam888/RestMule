import "../rescli.eol";

var api = RESCLI!API.all.first();

// ADDING AUTH 

api.auth.add(new RESCLI!NoAuth);
api.auth.add(new RESCLI!BasicAuth);

// RATE LIMITS

var search = new RESCLI!RatePolicyScope;
search.scope = "Search";
var entity = new RESCLI!RatePolicyScope;
entity.scope = "Entity";

// SEARCH Authenticated

var policyA = new RESCLI!Policy;
policyA.limit = 30;
policyA.auths.add(api.auth.select(a|a.instanceOf(RESCLI!OAuth)).first);
policyA.auths.add(api.auth.select(a|a.instanceOf(RESCLI!BasicAuth)).first);
search.policies.add(policyA);

// SEARCH Unauthenticated

var policyB = new RESCLI!Policy;
policyB.limit = 10;
policyB.auths.add(api.auth.select(a|a.instanceOf(RESCLI!NoAuth)).first);
search.policies.add(policyB);

// ENTITY Authenticated 

var policyC = new RESCLI!Policy;
policyC.limit = 5000;
policyC.auths.add(api.auth.select(a|a.instanceOf(RESCLI!OAuth)).first);
policyC.auths.add(api.auth.select(a|a.instanceOf(RESCLI!BasicAuth)).first);
entity.policies.add(policyC);

// ENTITY Unauthenticated

var policyD = new RESCLI!Policy;
policyD.limit = 1000;
policyD.auths.add(api.auth.select(a|a.instanceOf(RESCLI!NoAuth)).first);
entity.policies.add(policyD);

// RATE POLICY

var policy = new RESCLI!RatePolicy;
policy.scopes.add(entity);
policy.scopes.add(search);

var reset = new RESCLI!ResponseHeader;
var resetInt = new RESCLI!TInteger;
resetInt.label = "X-RateLimit-Reset";
reset.type = resetInt;
policy.reset = reset;

var limit = new RESCLI!ResponseHeader;
var limitInt = new RESCLI!TInteger;
limitInt.label = "X-RateLimit-Limit";
limit.type = limitInt;
policy.limit = limit;

var remaining = new RESCLI!ResponseHeader;
var remainingInt = new RESCLI!TInteger;
remainingInt.label = "X-RateLimit-Remaining";
remaining.type = remainingInt;
policy.remaining = remaining;

api.ratePolicy = policy;

// PAGINATION

var pagination= new RESCLI!PaginationPolicy;
pagination.start = 1;
pagination.max = 10;
pagination.increment = 1;
pagination.maxPerIteration = 100;

var perIteration = new RESCLI!Query; 
perIteration.description = "Items per page";
perIteration.required = false;
var type = new RESCLI!TInteger;
type.label = "per_page";
type.name = type.label;
perIteration.type = type;
pagination.perIteration = perIteration;

var page = new RESCLI!Query;
page.description = "Page identifier";
page.required = false;
var type1 = new RESCLI!TInteger;
type1.label = "page";
type1.name = type1.label;
page.type = type1;
pagination.page = page;

var link = new RESCLI!ResponseHeader;
link.description = "Page links";
var format = new RESCLI!TFormattedString;
format.label = "Link";
format.name = format.label;
link.type = format;
pagination.links = link;

api.pagination = pagination; 

// WRAPPER

var wrapper = new RESCLI!Wrapper;
wrapper.name = "Wrapper";
var items = new RESCLI!ListType;
items.label = "items";
wrapper.items = items;
wrapper.totalLabel= "total_count";
wrapper.incompleteLabel = "incomplete_results";
api.pageWrapper = wrapper;

// ADD RATE & WRAPPER TO REQUESTS (FIXME)

for (r in RESCLI!Request.all){
	if (r.parent.path.startsWith("/search")){
		r.scope = search;
	} else {
		r.scope = entity;
	}
	r.parameters.removeAll(r.parameters.select(p|p.instanceOf(RequestHeader)));
	for (resp in r.responses.select(s|s.responseType <> null)){
		resp.unwrap();
	}
}

/*  //////////
 * OPERATIONS
 *//////////
operation RESCLI!ObjectType hasWrapper() : Boolean{
	var wrapper = RESCLI!Wrapper.all.first;
	var lists = self.listFields.collect(a|a.label);
	return (not lists.isEmpty()) and lists
		.includes(wrapper.items.label);
}

operation RESCLI!Response unwrap() : RESCLI!ObjectType{
	if (self.responseType.instanceOf(ObjectType)){
		if (self.responseType.hasWrapper()){
			("Unwrapping : "+ self.responseType.name).println;
			var wrapper = RESCLI!Wrapper.all.first;
			var name = self.responseType.name.println;
			self.responseType = self.responseType.println.listFields
				.select(b| b.label == wrapper.items.label).first.elements.first;			
			self.responseType.name = name;
			self.pageWrapped = true;
			self.responseType.description = "UNWRAPPED: " + self.responseType.description;
		}
	}
}